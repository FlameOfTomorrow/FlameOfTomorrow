EventPointerTable(PrologueEvent, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN

ALIGN 32
CharacterBasedEvents:
END_MAIN

MiscBasedEvents:
AREA 0x18 NPCChecker [0,0] [16,21]
AFEV 0x14 Door1Open 0x10
AFEV 0x15 Door2Open 0x11
AFEV 0x16 Door3Open 0x12
AFEV 0x17 Door4Open 0x13
CauseGameOverIfLordDies
END_MAIN

NPCChecker:
CHECK_OTHERS
BNE 1 0x0 0xC //branch label 1 if there are NPCs left
CALL ActivateSeize
GOTO 2
LABEL 1 //if there are NPCs left
ENUF 0x18 //allow event to repeat
LABEL 2
NoFade
ENDA

ActivateSeize:
ENUT 0x19 //allow seize to end chapter
Text(HouseText)
ENDA

Door1Open:
Text(HouseText)
SetAI(ProloguePrisoner1,0x0,0xC) // Escape AI
NoFade
ENDA

Door2Open:
Text(HouseText)
SetAI(Merry,0x0,0xC) // Escape AI
NoFade
ENDA

Door3Open:
Text(HouseText)
SetAI(ProloguePrisoner3,0x0,0xC) // Escape AI
NoFade
ENDA

Door4Open:
Text(HouseText)
SetAI(ProloguePrisoner4,0x0,0xC) // Escape AI
NoFade
ENDA

LocationBasedEvents:
Seize(2,1)
DoorFlag(0x10,2,11)
DoorFlag(0x11,6,5)
DoorFlag(0x12,6,8)
DoorFlag(0x13,11,7)
Door(13,3)
Door(14,1)
Door(14,11)
Door(16,7)
Chest(IronSword,10,0)
Chest(IronSword,11,1)
Chest(IronSword,14,9)
Chest(IronSword,16,10)
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Units1:
UNIT Artemer Huntsman 0 Level(1,Ally,0) [8,18] 0 0 0 0 [IronBow,HuntingBow,Vulnerary,MasterKey] NoAI
UNIT Helena Troubadour 0 Level(1,Ally,0) [7,18] 0 0 0 0 [Unlock] NoAI
UNIT

EnemyUnits:
UNIT ONeill Fighter 0 Level(1,Enemy,1) [2,1] 0 0 0 0 [IronAxe] NeverMoveAI
UNIT 0x80 Cavalier 0 Level(1,Enemy,1) [8,19] 2 0 0 0 [Item100G] NeverMoveAI
UNIT

Prisoners:
UNIT ProloguePrisoner1 Civilian2 0 Level(1,NPC,0) [2,9] 0 0 0 0 [0] GuardTileAI
UNIT ProloguePrisoner1 CivilianF 0 Level(1,NPC,0) [1,10] 0 0 0 0 [0] GuardTileAI
UNIT Merry Outlaw 0 Level(1,NPC,0) [4,6] 0 0 0 0 [IronBow] DoNothingAI
UNIT ProloguePrisoner3 CivilianF2 0 Level(1,NPC,0) [4,9] 0 0 0 0 [0] GuardTileAI
UNIT ProloguePrisoner3 Civilian 0 Level(1,NPC,0) [4,8] 0 0 0 0 [0] GuardTileAI
UNIT ProloguePrisoner4 Civilian2F 0 Level(1,NPC,0) [12,9] 0 0 0 0 [0] GuardTileAI
UNIT ProloguePrisoner4 Civilian2 0 Level(1,NPC,0) [12,8] 0 0 0 0 [0] GuardTileAI
UNIT

Traps1:
GreenSigil(7,21)
GreenSigil(8,21)
GreenSigil(9,21)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

GotoDebugRoom:
MNC2 0x37 // debugroom (lagdou 10)
ENDB

BeginningScene:
// check for debug room
#ifdef _DEBUGROOM_
	CALL GotoDebugRoom
#endif

LOAD1 0x1 Units1
ENUN
LOAD1 0x1 EnemyUnits
ENUN
LOAD1 0x1 Prisoners
ENUN
GotoPrepScreen
ENDA

EndingScene:
MNC2 0x1
NoFade
ENDA